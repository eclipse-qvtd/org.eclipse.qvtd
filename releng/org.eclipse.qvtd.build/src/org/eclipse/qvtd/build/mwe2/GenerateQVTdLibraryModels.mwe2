/*******************************************************************************
 * Copyright (c) 2015 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *   E.D.Willink - initial API and implementation
 *
 * Run As->MWE2 Workflow to regenerate the pivot model.
 *******************************************************************************/
module GenerateQVTdLibraryModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the QVTbase and QVTimperative Library Models.
 */
Workflow {
	bean = ResourceSetImpl : resourceSet {}
    bean = StandaloneSetup { resourceSet = resourceSet
    	platformUri = "../../plugins"
//    	scanClassPath = true
    }
    bean = GenModelSetup { resourceSet = resourceSet }
    component = GenerateOCLstdlibXtend { resourceSet = resourceSet
    	projectName = "org.eclipse.qvtd.pivot.qvtbase"
    	modelFile = "model/QVTbaseLibrary.oclstdlib"
    	uri = "http://www.eclipse.org/qvt/2015/QVTbaseLibrary"
    	ecoreFile = "/org.eclipse.qvtd.pivot.qvtbase/model-gen/QVTbaseLibrary.ecore"
    	javaFolder = "/org.eclipse.qvtd.pivot.qvtbase/emf-gen"
    	javaPackageName = "org.eclipse.qvtd.pivot.qvtbase.model"
    	javaClassName = "QVTbaseLibrary"
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Pivot"
    		to = "org.eclipse.ocl.pivot.model.OCLmetamodel"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Library"
    		to = "org.eclipse.ocl.pivot.model.OCLstdlib"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/qvt/2015/QVTbase"
    		to = "org.eclipse.qvtd.pivot.qvtbase.model.QVTbaseMetamodel"
    	}
    }
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/org.eclipse.qvtd.pivot.qvtbase/model/QVTbaseLibrary.genmodel"
    }
    component = Model2tablesGenerator { resourceSet = resourceSet
    	genModelFile = "/org.eclipse.qvtd.pivot.qvtbase/model/QVTbaseLibrary.genmodel"
    	genOCLstdlib = true
    }
    component = GenerateOCLstdlibXtend { resourceSet = resourceSet
    	projectName = "org.eclipse.qvtd.pivot.qvtimperative"
    	modelFile = "model/QVTimperativeLibrary.oclstdlib"
    	uri = "http://www.eclipse.org/qvt/2015/QVTimperativeLibrary"
    	ecoreFile = "/org.eclipse.qvtd.pivot.qvtimperative/model-gen/QVTimperativeLibrary.ecore"
    	javaFolder = "/org.eclipse.qvtd.pivot.qvtimperative/emf-gen"
    	javaPackageName = "org.eclipse.qvtd.pivot.qvtimperative.model"
    	javaClassName = "QVTimperativeLibrary"
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Pivot"
    		to = "org.eclipse.ocl.pivot.model.OCLmetamodel"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Library"
    		to = "org.eclipse.ocl.pivot.model.OCLstdlib"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/qvt/2015/QVTimperative"
    		to = "org.eclipse.qvtd.pivot.qvtimperative.model.QVTimperativeMetamodel"
    	}
    }
    component = GenerateModel { resourceSet = resourceSet skipOnErrors = true
    	genModel = "/org.eclipse.qvtd.pivot.qvtimperative/model/QVTimperativeLibrary.genmodel"
    }
    component = Model2tablesGenerator { resourceSet = resourceSet
    	genModelFile = "/org.eclipse.qvtd.pivot.qvtimperative/model/QVTimperativeLibrary.genmodel"
    	genOCLstdlib = true
    }
}

