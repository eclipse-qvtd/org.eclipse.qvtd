/*******************************************************************************
 * Copyright (c) 2015 Willink Transformations and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v2.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v20.html
 *
 * Contributors:
 *   E.D.Willink - initial API and implementation
 *
 * Run As->MWE2 Workflow to regenerate the pivot model.
 *******************************************************************************/
module GenerateQVTdLibraryModels

import org.eclipse.ocl.examples.build.utilities.*
import org.eclipse.ocl.examples.build.xtend.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.mwe.utils.*
import org.eclipse.emf.ecore.resource.impl.*

/**
 *	Generate the QVTbase and QVTimperative Library Models.
 */
Workflow {
	bean = ResourceSetImpl : libResourceSet {}
    bean = StandaloneSetup { resourceSet = libResourceSet
    	platformUri = "../../plugins"
//    	scanClassPath = true
    }
	bean = ProjectMapSetup { resourceSet = libResourceSet }
    bean = EcoreGenModelSetup { resourceSet = libResourceSet }
    component = GenerateOCLstdlibXtend { resourceSet = libResourceSet
    	projectName = "org.eclipse.qvtd.runtime"
    	modelFile = "model/QVTruntimeLibrary.oclstdlib"
    	uri = "http://www.eclipse.org/qvt/2019/QVTruntimeLibrary"
    	ecoreFile = "/org.eclipse.qvtd.runtime/model-gen/QVTruntimeLibrary.ecore"
    	javaFolder = "/org.eclipse.qvtd.runtime/emf-gen"
    	javaPackageName = "org.eclipse.qvtd.runtime.model"
    	javaClassName = "QVTruntimeLibrary"
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Pivot"
    		to = "org.eclipse.ocl.pivot.model.OCLmetamodel"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Library"
    		to = "org.eclipse.ocl.pivot.model.OCLstdlib"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/qvt/2015/QVTbase"
    		to = "org.eclipse.qvtd.pivot.qvtbase.model.QVTbaseMetamodel"
    	}
    	excludedEClassifierName = "PseudoOperations"
    //	excludedEClassifierName = "Extent"
    //	excludedEClassifierName = "Model"
    //	excludedEClassifierName = "Transformation"
    	useOCLstdlib = true
    	isASLibrary = false
    }
	bean = ResourceSetImpl : qvtbResourceSet {}
	bean = ProjectMapSetup { resourceSet = qvtbResourceSet }
    component = GenerateModel { resourceSet = qvtbResourceSet skipOnErrors = true
    	genModel = "/org.eclipse.qvtd.runtime/model/QVTruntimeLibrary.genmodel"
    }
    component = Model2tablesGenerator { resourceSet = qvtbResourceSet
    	genModelFile = "/org.eclipse.qvtd.runtime/model/QVTruntimeLibrary.genmodel"
    	genOCLstdlib = true
    }
    component = GenerateOCLstdlibXtend { resourceSet = libResourceSet
    	projectName = "org.eclipse.qvtd.pivot.qvtimperative"
    	modelFile = "model/QVTimperativeLibrary.oclstdlib"
    	uri = "http://www.eclipse.org/qvt/2016/QVTimperativeLibrary"
    	ecoreFile = "/org.eclipse.qvtd.pivot.qvtimperative/model-gen/QVTimperativeLibrary.ecore"
    	javaFolder = "/org.eclipse.qvtd.pivot.qvtimperative/emf-gen"
    	javaPackageName = "org.eclipse.qvtd.pivot.qvtimperative.model"
    	javaClassName = "QVTimperativeLibrary"
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Pivot"
    		to = "org.eclipse.ocl.pivot.model.OCLmetamodel"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/ocl/2015/Library"
    		to = "org.eclipse.ocl.pivot.model.OCLstdlib"
    	}
    	generatedClassNameMap = {
    		from = "http://www.eclipse.org/qvt/2016/QVTimperative"
    		to = "org.eclipse.qvtd.pivot.qvtimperative.model.QVTimperativeMetamodel"
    	}
    	excludedEClassifierName = "PseudoOperations"
    	excludedEClassifierName = "Extent"
    	excludedEClassifierName = "Model"
    	excludedEClassifierName = "Transformation"
    	isASLibrary = false
    }
	bean = ResourceSetImpl : qvtiResourceSet {}
	bean = ProjectMapSetup { resourceSet = qvtiResourceSet }
    component = GenerateModel { resourceSet = qvtiResourceSet skipOnErrors = true
    	genModel = "/org.eclipse.qvtd.pivot.qvtimperative/model/QVTimperativeLibrary.genmodel"
    }
    component = Model2tablesGenerator { resourceSet = qvtiResourceSet
    	genModelFile = "/org.eclipse.qvtd.pivot.qvtimperative/model/QVTimperativeLibrary.genmodel"
    	genOCLstdlib = true
    }
}

