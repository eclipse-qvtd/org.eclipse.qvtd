[% for (i in QVTc!Model.all.first().ownedImports) { %]
import [%=i.name%]: '[%=i.importedNamespace.URI%]';
[% } %]

[% for (t in QVTc!Transformation.all()) { %]
transformation [%=t.name %] {
  [%for (mp in t.modelParameter) { %]
    [%=mp.name%] imports [%=mp.usedPackage.collect(up | up.name).concat(",")%]; 
  [%}%]
}

  [%for (r in t.rule) {
        r.declaration(t);
    }
}%]

[% operation QVTc!Mapping declaration(t : QVTc!Transformation) { %]
map [%if (self.name.isDefined()){%][%=self.name%][%if (t.isDefined()){%] in [%=t.name%][%}}%] {
[% for (d in self.domain) {
    var domainName = "";
    if (d.isCheckable) { domainName = "check ";}
    if(d.isEnforceable) { domainName = domainName + "enforce ";}
    domainName = domainName + d.name;%]
    [%=domainName%] ([%d.guardPattern.declaration();%]) {
    [%d.bottomPattern.declaration();%]
    }
    [%
}%]
    where ([%self.guardPattern.declaration();%]) {
    [%self.bottomPattern.declaration();%]
    }
  [%for (l in self.local) {
        l.declaration(null);%]

  [%}%]
}
[%}%]

[% operation QVTc!GuardPattern declaration() {%]
[%  if (not self.variable.isEmpty()) {
        for (v in self.variable.select(v | v.isTypeOf(QVTc!Variable))){%]
    [%=v.toString()%][%if(hasMore){%],
[%out.print("        ");}%]
      [%}%]
      |[%
    }  
    for (p in self.predicate){%]
        
        [%=p.toString()%];[%
    }%]
[%} %] 

[% operation QVTc!BottomPattern declaration() {
    var vars : Set();
    vars.addAll(self.variable);
    vars.addAll(self.realizedVariable);
    for (v in vars.sortBy(v | v.type())) {%]
[%printIdent(8);%][%=v.toString()%][%if(hasMore){%],
[%}%]
      [%}%]  |[% 
    for (p in self.predicate){%]
    
        [%=p.toString()%];[%
    }
    for (a in self.assignment){%]
    
        [%=a.toString()%];[%
    }%]
[%} %]

[% operation printIdent(size : Integer) {
out.print("".pad(size, " ", true));
}%]
