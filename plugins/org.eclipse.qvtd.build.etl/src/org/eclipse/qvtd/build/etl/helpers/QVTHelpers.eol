/*******************************************************************************
 * Copyright (c) 2013 The University of York and Willink Transformations.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * 
 * Contributors:
 *     Horacio Hoyos - initial implementation
 ******************************************************************************/
import "OCLExpressionAnalysis.eol";

/****l* /org.eclipse.qvtd.build.etl/helpers/QVTHelpers
 * NAME
 *  QVTHelpers -- A set of operations to query, format, interpret, etc., 
 *  information from a QVT model. 
 * NOTES
 *  Some of this operations can/should be moved to the QVTcore model/code 
 */

/****o* QVTHelpers/isInputDomain
 * NAME
 *  isInputDomain - Returns true if the area is of type QVT!CoreDomain and it's 
 *  TypedModel is the input domain.
 *
 * CONTEXT
 *  QVT!Area  The are under test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  QVTHelpers/isMiddleDomain
 *  QVTHelpers/isOutputDomain
 */
 // TODO the configuration should really point to the specific package
@chached
operation QVT!Area isInputDomain() : Boolean {
    return self.isTypeOf(QVT!CoreDomain) and
        config!Configuration.all.first().inputDirection.name == self.typedModel.name;
}

/****o* QVTHelpers/isMiddleDomain
 * NAME
 *  isMiddleDomain - Returns true if the area belongs to the middle domain, i.e.
 *  it is a QVT!Mapping.
 *
 * CONTEXT
 *  QVT!Area  The are under test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  QVTHelpers/isInputDomain
 *  QVTHelpers/isOutputDomain
 */
@chached
operation QVT!Area isMiddleDomain() : Boolean {
    return self.isTypeOf(QVT!Mapping);
}

/****o* QVTHelpers/isOutputDomain
 * NAME
 *  isOutputDomain - Returns true if the area is of type QVT!CoreDomain and it's 
 *  TypedModel is the output domain.
 *
 * CONTEXT
 *  QVT!Area  The are under test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  QVTHelpers/isInputDomain
 *  QVTHelpers/isMiddleDomain
 */
@chached
operation QVT!Area isOutputDomain() : Boolean {
    return self.isTypeOf(QVT!CoreDomain) and
        config!Configuration.all.first().outputDirection.collect(od | od.name).includes(self.typedModel.name);
}

@cached
operation QVT!OCLExpression isFromInputDomain() : Boolean {
    return self.getArea().isInputDomain();
}

@cached
operation QVT!OCLExpression isFromMiddleDomain() : Boolean {
    return self.getArea().isMiddleDomain();
}

@cached
operation QVT!OCLExpression isFromOutputDomain() : Boolean {
    return self.getArea().isOutputDomain();
}

/****o* QVTHelpers/isCheckMode
 * NAME
 *  isCheckMode - Indicates if the transformation is invoked in check mode.
 * ANNOTATIONS
 *  cached
 * RESULT
 *  True if the condifuration mode is Mode#check
 * SEE ALSO
 *  QVTHelpers/isEnforceMode
 */
@chached 
operation isCheckMode() : Boolean {
    return config!Configuration.all.first().mode == config!Mode#check;
}

/****o* QVTHelpers/isEnforceMode
 * NAME
 *  isEnforceMode - Indicates if the transformation is invoked in enforce mode.
 * ANNOTATIONS
 *  cached
 * RESULT
 *  True if the condifuration mode is Mode#enforcement
 * SEE ALSO
 *  QVTHelpers/isCheckMode
 */
@chached
operation isEnforceMode() : Boolean {
    return config!Configuration.all.first().mode == config!Mode#enforce;
}

/****o* QVTHelpers/getMiddleTypedModel
 * NAME
 *  getMiddleTypedModel - Returns the TypedModel reference for the middle domain
 * ANNOTATIONS
 *  cached
 * RESULT
 *  A reference to the TypeModel of the middle domain.
 */
@cached
operation QVT!Transformation getMiddleTypedModel() : QVT!TypedModel {
    
    if (self.modelParameter.one(tm | not tm.name.isDefined())) {
        return self.modelParameter.selectOne(tm | not tm.name.isDefined());
    } else {
        return self.modelParameter.selectOne(tm | tm.name = config!Configuration.all.first().middle.name);
    }
    
}

/****o* QVTHelpers/oppositePattern
 * NAME
 *  oppositePattern - Returns the pattern to which a variable belongs
 * ANNOTATIONS
 *  cached
 * RESULT
 *  The core pattern in which the variable is instanciated/referenced
 */
@cached
operation QVT!Variable oppositePattern() : QVT!CorePattern  {
    
   	if (self.eContainer().isKindOf(QVT!CorePattern))
   		return self.eContainer();
	return null;
}

/****o* QVTHelpers/getArea
 * NAME
 *  getArea - Returns the area that defines the context of an OCL Expression.
 * FUNCTION
 *  The area of an OCL Expression depends on the area of the variables used
 *  in the expression, and thus all the parts of the expression are inspected
 *  to find variable references and then their area
 * ANNOTATIONS
 *  cached
 * RESULT
 *  The core pattern in which the variable is instanciated/referenced
 * TODO
 *  What if the OCL Ecpression contains references to multiple variables in 
 *  multiple areas? return a collection and let the caller decide?
 */
@cached
operation QVT!OCLExpression getArea() : QVT!Area {
	var retVal : QVT!OCLExpression; 
    if (self.isTypeOf(QVT!VariableExp)) {
    	if (self.referredVariable.type() == null) {
    		self.println();
        	throw "Error! All variables should have types?" + self.type();
            //return null;
        }
        return self.referredVariable.oppositePattern().area;
    } else if (self.isKindOf(QVT!CallExp)) {
    	// TODO Calling operations in the context is trycky, analyze this!
        return self.ownedSource.getArea();
    } else if (self.isTypeOf(QVT!IfExp)) {
        return self.ownedCondition.getArea();
    } else if (self.isKindOf(QVT!LiteralExp)) {
        return null;
    } else {
        self.println();
        throw "Error! Add getArea for " + self.type();
    }
    return null;
}

/****o* QVTHelpers/findReferencedVariables
 * NAME
 *  findReferencedVariables - Returns the collection of all varaibles referenced
 *  in the assignment.
 * CONTEXT
 *  QVT!Assignment
 * ANNOTATIONS
 *  cached
 * RESULT
 *  A collection of all the varaibles found.
 */
operation QVT!Assignment findReferencedVariables() : Sequence {
    
    var varRefs : Sequence();
    varRefs = self.value.findReferencedVariables();
    if (self.isTypeOf(QVT!PropertyAssignment)) {
        varRefs.addAll(self.slotExpression.findReferencedVariables());
    } else if (self.isTypeOf(QVT!VariableAssignment)) {
        varRefs.add(self.targetVariable);
    }
    return varRefs;
}

/****o* QVTHelpers/findReferencedVariables
 * NAME
 *  findReferencedVariables - Returns the collection of all varaibles referenced
 *  in the assignment.
 * CONTEXT
 *  QVT!Predicate
 * ANNOTATIONS
 *  cached
 * RESULT
 *  A collection of all the varaibles found.
 */
operation QVT!Predicate findReferencedVariables() : Sequence {
    return self.conditionExpression.findReferencedVariables();
}

/****o* QVTHelpers/retypeVar
 * NAME
 *  retypeVar - Retypes a variable to the less abstract type of all variables, in
 * the pattern, with the same name.
 * CONTEXT
 *  QVT!Variable
 * PARAMETERS
 *  pattern     The pattern to search for variables with the same name
 */
operation QVT!Variable retypeVar(pattern : QVT!Pattern) {
    var refinedVars = pattern.variable.select(
                    v | v.name == self.name
                    );
    if (refinedVars.size() > 0) {
        //var x = new Native("org.eclipse.qvtd.build.etl.PivotUtil");
        //var lrv = x.getLowestRankVariable(refinedVars.including(self));
        var lrv = getLowestRankVariable(refinedVars.including(self));
        self.type = lrv.type;
    }
}

/****o* QVTHelpers/retypeRealizedVar
 * NAME
 *  retypeRealizedVar - Retypes a RealizedVariable to the less abstract type of
 *  all realized variables, in the pattern, with the same name.
 * CONTEXT
 *  QVT!RealizedVariable
 * PARAMETERS
 *  pattern     The pattern to search for variables with the same name
 */
operation QVT!RealizedVariable retypeRealizedVar(pattern : QVT!Pattern) {
    var refinedVars = pattern.realizedVariable.select(
                    v | v.name == self.name
                    );
    if (refinedVars.size() > 0) {
        //var x = new Native("org.eclipse.qvtd.build.etl.PivotUtil");
        //var lrv = x.getLowestRankVariable(refinedVars.including(self));
        var lrv = getLowestRankVariable(refinedVars.including(self));
        self.type = lrv.type;
    }
}

/****o* QVTHelpers/getLowestRankVariable
 * NAME
 *  getLowestRankVariable - Finds the variable with the less abstract type from
 *  the collection of variables
 * PARAMETERS
 *  vars     The collection to perform the search
 */
operation getLowestRankVariable(vars : Collection) : QVT!Variable {
        
        var min = vars.first();
        for(v in vars.excluding(min)) {
            //minType = (ClassImpl) min.eGet(typeFeat);
            if (v.type.superClasses.includes(min.type)) {
                min = v;    
            }
        }
        // Verify that the min is actually the min, i.e. all the other variables are superiors
        // in the hierarchy
        /*for (TypedElement var : vars) {
            if (!var.equals(min)) {
                if (!((ClassImpl)min.eGet(typeFeat)).getSuperClass().contains(var.eGet(typeFeat))) {
                    System.out.println("min is not min");
                    return null;
                }
            }
        }*/
        return min;
}

/****o* QVTHelpers/isContainer
 * NAME
 *  isContainer - Indicates if the property is a containment relation
 *  by name 
 * CONTEXT
 *  QVT!Property  The property
 * ANNOTATIONS
 *  cached
 */
@cached
operation QVT!Property isContainer() : Boolean {
    if (self.getOpposite().isDefined()) {
        return self.getOpposite().isComposite();
    } else {
        return false;
    }
}

/****o* QVTHelpers/hasNavigableOpposite
 * NAME
 *  hasNavigableOpposite - Indicates if the property has a navigable opposite
 * FUNCTION
 *  The property has a navigable opposite if the opposite relation is defined
 *  and if that relation has a many multiplicity.   
 * CONTEXT
 *  QVT!Property  The property
 * ANNOTATIONS
 *  cached
 */
operation hasNavigableOpposite(prop) : Boolean {
    return prop.opposite.isDefined() and not prop.opposite.isIsMany(); 
}

/****o* QVTHelpers/getDataTypeUses
 * NAME
 *  getDataTypeUses - Returns a sequence of {Type, Attribtye} pairs whos
 *  value is assigned from a DataType.
 * FUNCTION
 *  Identifies all the assignments in which the value part is a variable whos type
 *  is a DataType and for each, it creates a {Type, Attribtye} pair that saves the 
 *  information about what attribute of what Type was being assigned. 
 * CONTEXT
 *  QVT!Mapping  The mapping under test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *
 */    
operation QVT!Mapping hasDataTypeUse(values : Sequence) : Boolean {
    var pivotUtil = new Native("org.eclipse.qvtd.build.etl.PivotUtil");
    //self.name.println();
    // Get all the assignments that hava a value that is a PropertyCallExp in which
    // the source is of the type and the referredProperty is the same
    if (self.getAllAssignments().exists(a | a.value.isTypeOf(QVT!PropertyCallExp)
            and a.value.ownedSource.isTypeOf(QVT!VariableExp) and pivotUtil.isKindOf(values.first(), a.value.ownedSource.type) 
            and a.value.referredProperty == values.last())) {
        return true;        
    }
    for (p in self.getAllPredicates()) {
        // Any of the arguments can be the use
        if (p.conditionExpression.isKindOf(QVT!OperationCallExp)) {
            //p.conditionExpression.toString().println();
            for (a in p.conditionExpression.ownedArguments.select(a | a.isTypeOf(QVT!PropertyCallExp))) {
                if (a.ownedSource.type == values.first() and a.referredProperty == values.last()) {
                    return true;
                }
            }
            if (pivotUtil.isKindOf(values.first(), p.conditionExpression.ownedSource.ownedSource.type ) and p.conditionExpression.ownedSource.referredProperty == values.last()) {
                return true;
            }
        }
    }
    return false;
}    

/****o* QVTHelpers/hasRelation
  *  NAME
  *   hasRelation -- Identify relations between variables
  *  SYNOPSIS
  *   relations =  var1.hasRelation(var2);
  *  FUNCTION
  *   Analysies the predicates and assignments of the mapping that contains the 
  *   variables and looks for relations of the form:  var1.att = var2. If found,
  *   the operation returns the attribute that relates the variables.    
  *  CONTEXT
  *     QVT!Variable
  *  PARAMETERS
  *    variable - variable to analyze relations against: variable.att = self
  *  RESULT
  *    Any  - The EReference that relates the variables, null if not existant
  *   TODO
  *   It may be usefull to divide this into hasPredicateRelation and hasAssignmentRelation
  *   so we have more controll over what relations we are looking for.
  *
  */
operation QVT!Variable hasRelation(other : QVT!Variable) : Bag {
    
    // Fin predicates and assignemnts that only refer the two variables and
    // return the properties that relate them
    return self.oppositePattern().area.rule.guardPattern.predicate
        .select(p | p.conditionExpression.isSimpleAttributeNavigation()
                and p.conditionExpression.isArgumentVariable(other)
                and p.conditionExpression.isSourceVariable(self))
        .collect(p | p.conditionExpression.getReferredProperty())
        .includingAll(
            self.oppositePattern().area.rule.bottomPattern.assignment
            .select(a | a.isSimpleAttributeNavigation()
                and a.slotExpression.referredVariable == self
                and a.value.referredVariable == other)
            .collect(a | a.targetProperty())
        );
}

/****o* QVTHelpers/getUnidirComment 
 * NAME
 *  getUnidirComment - Returns a string that contains a comment to state the
 *  direction and mode in which the transformation was invoked
 */ 
operation getUnidirComment() : String {
    var comment : String = "";
    if (config!Configuration.all.first().mode == config!Mode#enforce) {
        comment = comment + "Enforce ";
    } else {
        comment = comment + "Check ";
    }
    comment = comment + config!Configuration.all.first().inputDirection.name + " to ";
    comment = comment + config!Configuration.all.first().outputDirection.collect(d | d.name).concat(",") + "";
    return comment; 
}

// ========================================================= move to qvtimperative model

/** 
 * Returns the tranformation of a mapping. Does a bottom up recursive search
 */
operation QVT!Mapping getTransforamtion() : QVT!Transformation {
    if (self.transformation.isDefined()) {
        return self.transformation;
    } else if (self.context.isDefined()) {
        return self.context.getTransforamtion();
    } else {
        self.err("Cotextless mapping ");
    }
}
 /* this can be getAllPatterns, see how is is called for all atributes with super classes */
@cached
operation QVT!Area getContextMappingPatterns() : Sequence {

    var mapping;
    if (self.isTypeOf(QVT!CoreDomain)) {
        mapping = self.rule;
    } else if (self.isTypeOf(QVT!Mapping)) {
        mapping = self;
    }
    var searchPatterns = new Sequence;
    if (mapping.context.isDefined()) {
        searchPatterns.addAll(mapping.context.domain.collect(d | d.guardPattern).flatten());
        searchPatterns.addAll(mapping.context.domain.collect(d | d.bottomPattern).flatten());
        searchPatterns.add(mapping.context.guardPattern);
        searchPatterns.add(mapping.context.bottomPattern);
        searchPatterns.addAll(mapping.context.getContextMappingPatterns());
    }
    return searchPatterns;

}

/****o* QVTHelpers/getInputVariables
 * NAME
 *  getInputVariables - Returns the input variables of the Mapping, excluding DataType
 *  variables
 *
 * CONTEXT
 *  QVT!Rule  The mapping
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/getOutputVariables
 */
@cached
operation QVT!Rule getInputVariables() : Sequence {
    
    var vars: Sequence;
    vars = self.guardPattern.variable
        .includingAll(self.domain.collect(d | d.guardPattern).flatten().collect(gp | gp.variable).flatten()
                     ).asSequence();
    //for (l in self.local) {
    //    vars = vars.includingAll(l.getInputVars());
    //}
    // QVTc, QVTu, QVTm: Do the same for all refined rules
    //for (r in self.refinement) {
    //    vars = vars.includingAll(r.getInputVars());
    //}
    return vars;
}

/****o* QVTHelpers/getOutputVariables
 * NAME
 *  getOutputVariables - Returns the output variables of the Mapping, excluding 
 *  DataType variables
 * FUNCTION
 *  It only works for QVTm/QVTi as is does not check for nested mappings. Variables
 *  of  PrimitiveTypes are ignored.
 * CONTEXT
 *  QVT!Rule  The mapping
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  QVTHelpers/getInputVariables
 */
@cached
operation QVT!Rule getOutputVariables() : Set {
    
    var vars: Set;
    vars = self.bottomPattern.realizedVariable
        .includingAll(self.domain.collect(d | d.bottomPattern).flatten().collect(gp | gp.realizedVariable).flatten());
    //for (l in self.local) {
    //    vars = vars.includingAll(l.getInputVars());
    //}
    // QVTc, QVTu, QVTm: Do the same for all refined rules
    //for (r in self.refinement) {
    //    vars = vars.includingAll(r.getInputVars());
    //}
    return vars;
}

operation QVT!Rule getAllVariables() {
    return self.getInputVariables().includingAll(self.getOutputVariables());
}

operation QVT!Transformation getAllVariables() {
}


/****o* QVTHelpers/getAllAssignments
 * NAME
 *  getAllAssignments - Returns all the assignments of the mapping
 *
 * CONTEXT
 *  QVT!Mapping  The mapping
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/
 */
 /* TODO Added the commented code to add it to QVTc/u/m model */
@cached
operation QVT!Mapping getAllAssignments() : Sequence {
    var asgs : Sequence;
    asgs.addAll(self.bottomPattern.assignment);
    for (d in self.domain) {
        asgs.addAll(d.bottomPattern.assignment);
    }
    //for (l in self.local) {
    //    asgs.addAll(l.getAllAssignments());
    //}
    return asgs;
}


/****o* QVTHelpers/getAllPredicates
 * NAME
 *  getAllPredicates - Returns all the predicates of the mapping
 *
 * CONTEXT
 *  QVT!Mapping  The mapping
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/
 */
/* TODO Added the commented code to add it to QVTc/u/m model */
@cached
operation QVT!Mapping getAllPredicates() : Sequence {
    var pdcts : Sequence;
    pdcts.addAll(self.bottomPattern.predicate);
    pdcts.addAll(self.guardPattern.predicate);
    for (d in self.domain) {
        pdcts.addAll(d.bottomPattern.predicate);
        pdcts.addAll(d.guardPattern.predicate);
    }
    //for (l in self.local) {
    //    pdcts.addAll(l.getAllPredicates());
    //}
    return pdcts;
}

/****o* QVTHelpers/getPrimitiveDataTypeAssignemnts
 * NAME
 *  getPrimitiveDataTypeAssignemnts - Returns all the property assignments of 
 *  the mapping were the value is a variable with a primitive data type, i.e.,
 *    varA.property = 'PrimitiveDataType'
 *
 * CONTEXT
 *  QVT!Mapping  The mapping
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/isPrimitivePropertyAssignemnt
 */
@cached
operation QVT!Mapping getPrimitiveDataTypeAssignemnts() : Sequence {
	
	return self.getAllAssignments()
		.select(a | a.isPrimitivePropertyAssignemnt());
}


/****o* QVTHelpers/isSimpleAttributeNavigation
 * NAME
 *  getAllPredicates - Determine if the Assignemnts is of the form 
 *    varA.attribute := varB
 *  1. The Assignment must be an Attribute assignment
 *  2. The slot operation must be a VariableExp
 *  3. The value must be VariableExp
 * 
 * CONTEXT
 *  QVT!Assignment  The assignement to test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/
 */
operation QVT!Assignment isSimpleAttributeNavigation() : Boolean {

	return self.isTypeOf(QVT!PropertyAssignment)
		and self.slotExpression.isTypeOf(QVT!VariableExp)
		and self.value.isTypeOf(QVT!VariableExp);
}

/****o* /org.eclipse.qvtd.build.etl/helpers/QVTHelpers/isPrimitivePropertyAssignemnt 
 * NAME
 *  isPrimitivePropertyAssignemnt - Determine if the Assignemnts assigns a primitive
 *  data type
 *  
 * CONTEXT
 *  QVT!Assignment  The assignement to test
 * ANNOTATIONS
 *  cached
 * SEE ALSO
 *  QVTHelpers/isSimpleAttributeNavigation
 */
operation QVT!Assignment isPrimitivePropertyAssignemnt() : Boolean {
    
    return self.isSimpleAttributeNavigation()
    	and self.value.type.isTypeOf(QVT!PrimitiveType);
}

@chached
operation QVT!Area isNested() : Boolean {
    if (self.isTypeOf(QVT!Mapping))
        return self.context.isDefined();
    // Else is a CoreDomain
    return self.rule.context.isDefined();
}

operation areEquivalentAssignments(assg1 : QVT!Assignment, assg2 : QVT!Assignment) : Boolean {
	// FIXME If we found an example decide if we need OCL to provide OCL expression comparision
	return false;
}


// ======================== Fix Variable References ===========================

// There are two bad references:
// 1. The variable belongs to another model
// 2. The variable belongs to another mapping
// TODO We really only neeed VariableExp?
// TODO check types/superTypes too?
operation QVT!VariableExp fixVarReferences() {
	//self.toString().println("fixCopiedVarReferences ");
	//self.getOwningMapping().println();
	var targetVar = self.getOwningMapping().getVariables().selectOne(v | v.name == self.referredVariable.name);
	//targetVar.println("targetVar ");
	self.referredVariable = targetVar;
}

@cached
operation QVT!Variable getOwningMapping() : QVT!Mapping {
	
	if (self.oppositePattern().area.isTypeOf(QVT!Mapping))
		return self.oppositePattern().area;
	else
		return self.oppositePattern().area.rule;
}

operation QVT!VariableExp getOwningMapping() : QVT!Mapping {
	
	return self.eContainer().closure(e | e.eContainer()).selectOne(m | m.isTypeOf(QVT!Mapping));
}



operation QVT!Mapping getVariables() : Sequence {
	
	var vars : Sequence();
	vars.addAll(self.guardPattern.variable);
	vars.addAll(self.bottomPattern.variable);
	vars.addAll(self.bottomPattern.realizedVariable);
	vars.addAll(self.domain.guardPattern.variable.flatten());
	vars.addAll(self.domain.bottomPattern.variable.flatten());
	vars.addAll(self.domain.bottomPattern.realizedVariable.flatten());
	return vars;
}

operation fixReferences(exps : Sequence) {
	/*QVTm!VariableExp.all().select(ve | not ve.referredVariable.eContainer().isTypeOf(QVT!IteratorExp)).size.println();
    QVTm!VariableExp.all()
    	.select(ve | not ve.referredVariable.eContainer().isTypeOf(QVT!IteratorExp)
    			and (ve.referredVariable.owningModel() <> ve.owningModel()
    				or ve.referredVariable.getOwningMapping() <> ve.getOwningMapping())).size().println("to fix ");
    */
    // Trailing variable references can only be fixed once all the elements are in place
    for (ve in exps
    	.select(ve | ve.referredVariable.oppositePattern().isDefined()				// Variables contained in CorePaterrn 
    			and (ve.referredVariable.owningModel() <> ve.owningModel()
    				or ve.referredVariable.getOwningMapping() <> ve.getOwningMapping())
    	)) {
    	ve.fixVarReferences();
    }
    /*QVTm!VariableExp.all()
    	.select(ve | not ve.referredVariable.eContainer().isTypeOf(QVT!IteratorExp)
    			and (ve.referredVariable.owningModel() <> ve.owningModel()
    				or ve.referredVariable.getOwningMapping() <> ve.getOwningMapping())).size().println("missing ");
	*/
}

