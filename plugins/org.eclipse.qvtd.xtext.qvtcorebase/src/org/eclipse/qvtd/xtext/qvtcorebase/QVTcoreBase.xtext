/*******************************************************************************
 * Copyright (c) 2011 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *******************************************************************************/
grammar org.eclipse.qvtd.xtext.qvtcorebase.QVTcoreBase with org.eclipse.ocl.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/ocl/2015/BaseCS" as base
import "http://www.eclipse.org/ocl/2015/EssentialOCLCS" as essentialocl
import "http://www.eclipse.org/ocl/2015/Pivot" as pivot
import "http://www.eclipse.org/qvt/2015/QVTbase" as qvtbase
import "http://www.eclipse.org/qvt/2015/QVTcoreBase" as qvtcorebase
import "http://www.eclipse.org/qvt/2015/QVTcoreBaseCS"
		
BottomPatternCS:
	( '{' (unrealizedVariables+=UnrealizedVariableCS | realizedVariables+=RealizedVariableCS)
		(',' (unrealizedVariables+=UnrealizedVariableCS | realizedVariables+=RealizedVariableCS))*
		('|' constraints+=PredicateOrAssignmentCS*)? '}'
	)
	| ( {BottomPatternCS} '{' constraints+=PredicateOrAssignmentCS* '}'
	)
;
	
DirectionCS: {DirectionCS} name=UnrestrictedName?
	('imports' imports+=[pivot::Package|UnrestrictedName] (',' imports+=[pivot::Package|UnrestrictedName])*)?
	('uses' uses+=[qvtcorebase::CoreDomain|UnrestrictedName] (',' uses+=[qvtcorebase::CoreDomain|UnrestrictedName])*)?
;
	
//EnforcementOperationCS: ('creation'|'deletion') ExpCS ';';
	
GuardPatternCS:
	({GuardPatternCS} '(' (unrealizedVariables+=GuardVariableCS (',' unrealizedVariables+=GuardVariableCS)*)?
	   ('|' ownedPredicates+=PredicateCS*)? ')' )
	|
	({GuardPatternCS} '(' (unrealizedVariables+=GuardVariableCS (',' unrealizedVariables+=GuardVariableCS)*)? ')' )	// FIXME Redundant but avoids serializer failure on Class2RDBMS.qvtc
	|
	({GuardPatternCS} '(' (ownedPredicates+=PredicateCS*) ')' )
;
	
//Variable := VariableName �:� TypeDeclaration
GuardVariableCS returns UnrealizedVariableCS: name=UnrestrictedName ':' ownedType=TypeExpCS;

ImportCS returns base::ImportCS:
	'import' (name=Identifier ':')? ownedPathName=URIPathNameCS (isAll?='::' '*')? ';';

//IncludeCS returns IncludeCS:
//	'include' namespace=[pivot::Namespace|URI] ';';

LibraryCS returns base::LibraryCS:
	'library' referredPackage=[pivot::Package|URI] ';';
	
NamedDomainCS returns DomainCS: check?='check'? enforce?='enforce'? direction=[qvtbase::TypedModel|UnrestrictedName]
	guardPattern=GuardPatternCS bottomPattern=BottomPatternCS;
	
ParamDeclarationCS: name=UnrestrictedName ':' ownedType=TypeExpCS;

PredicateCS: ownedCondition=ExpCS ';';
		
//Constraint ::= Predicate | Assignment
//Predicate ::= BooleanOCLExpr
//Assignement ::= [�default�] SlotOwnerOCLExpr�.�PropertyName �:=� ValueOCLExpr
PredicateOrAssignmentCS returns AssignmentCS: default?='default'? target=ExpCS (':=' initialiser=ExpCS)? ';';
		
QueryCS: 'query' pathName=ScopeNameCS name=UnrestrictedName
		'(' (inputParamDeclaration+=ParamDeclarationCS (',' inputParamDeclaration+=ParamDeclarationCS)*)? ')'
		':' ownedType=TypeExpCS (';' | '{' expression=ExpCS '}');
//<query> ::= 'query' <PathNameCS> 
//            '(' [<paramDeclaration> (',' <paramDeclaration>)*] ')'
//      	  ':' <TypeCS>
//            (';' | '{' <OCLExpressionCS> '}')

ScopeNameCS returns base::PathNameCS:
	ownedPathElements+=FirstPathElementCS '::' (ownedPathElements+=NextPathElementCS '::')*;

TransformationCS:
	'transformation' pathName=ScopeNameCS? name=UnreservedName '{' (directions+=DirectionCS ';')* '}';
	
//Variable := VariableName �:� TypeDeclaration
UnrealizedVariableCS: name=UnrestrictedName (':' ownedType=TypeExpCS)? (':=' ownedInitExpression=ExpCS)?;
		
//RealizedVariable := �realized� VariableName �:� TypeDeclaration
RealizedVariableCS: 'realize' name=UnrestrictedName ':' ownedType=TypeExpCS;
	
UnnamedDomainCS returns DomainCS: {DomainCS} guardPattern=GuardPatternCS bottomPattern=BottomPatternCS;
		
UnrestrictedName returns ecore::EString:
	EssentialOCLUnrestrictedName
|	'check'
//|	'creation'
//|	'default'
//|	'deletion'
|	'enforce'
|	'import'
|	'imports'
//|	'include'
|	'library'
|	'map'
|	'query'
|	'realize'
|	'refines'
|	'transformation'
|	'uses'
//|	'where'
;
