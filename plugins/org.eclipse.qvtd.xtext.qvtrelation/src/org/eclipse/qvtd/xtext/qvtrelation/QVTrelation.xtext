/*******************************************************************************
 * Copyright (c) 2011 E.D.Willink and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 *     E.D.Willink - initial API and implementation
 *******************************************************************************/
grammar org.eclipse.qvtd.xtext.qvtrelation.QVTrelation with org.eclipse.ocl.examples.xtext.essentialocl.EssentialOCL

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/ocl/3.1.0/BaseCST" as base
import "http://www.eclipse.org/ocl/3.1.0/EssentialOCLCST" as essentialocl
import "http://www.eclipse.org/ocl/3.1.0/Pivot" as pivot
import "http://www.eclipse.org/qvt/0.9/QVTbase" as qvtbase
import "http://www.eclipse.org/qvt/0.9/QVTrelation" as qvtrelation
import "http://www.eclipse.org/qvt/0.9/QVTrelationCST"

TopLevelCS:
	(ownedImports+=UnitCS | /*ownedInclude+=IncludeCS |*/ ownedLibraries+=LibraryCS)*
	transformations+=TransformationCS*	
;
		
//UnitCS: identifier+=[pivot::Element|UnrestrictedName] ('.' identifier+=[pivot::Element|UnrestrictedName])*;
	
CollectionTemplateCS: name=UnrestrictedName? ':' type=CollectionTypeCS
	'{' (memberIdentifiers+=(TemplateCS | ElementTemplateCS)
	    (',' memberIdentifiers+=(TemplateCS | ElementTemplateCS))*
        '++' (restIdentifier=[pivot::Variable|UnrestrictedName] | '_')                     
         )? '}';

DefaultValueCS: propertyId=[pivot::Variable|UnrestrictedName] '=' initialiser=ExpCS ';';

DomainCS: (checkonly?='checkonly' | enforce?='enforce' | 'replace')? 'domain' modelId=[qvtbase::TypedModel|UnrestrictedName] pattern=DomainPatternCS
             ('implementedby' implementedBy=ExpCS)?
             ('default_values' '{' (defaultValues+=DefaultValueCS)+ '}')?
             ';';

DomainPatternCS: template=TemplateCS;

ElementTemplateCS: name=UnrestrictedName | {ElementTemplateCS} '_';

KeyDeclCS: 'key' pathName=PathNameCS '{'
	(propertyIds+=[pivot::Property|UnrestrictedName]
	| ('opposite' '(' oppositePropertyIds+=PathNameCS ')'))
	(',' (propertyIds+=[pivot::Property|UnrestrictedName]
	| ('opposite' '(' oppositePropertyIds+=PathNameCS ')'))
	)* '}' ';'
;

LibraryCS returns base::LibraryCS:
	'library' referredPackage=[pivot::Package|URI] ';';
	
ModelDeclCS: name=UnrestrictedName ':' (metaModelIds+=[pivot::Namespace|UnrestrictedName]
	| '{' metaModelIds+=[pivot::Namespace|UnrestrictedName] (',' metaModelIds+=[pivot::Namespace|UnrestrictedName])* '}')
;
	
ObjectTemplateCS: name=UnrestrictedName? ':' type=TypedRefCS
	'{' (propertyTemplates+=PropertyTemplateCS (',' propertyTemplates+=PropertyTemplateCS)*)? '}';
	
ParamDeclarationCS: name=UnrestrictedName ':' ownedType=TypedRefCS;	

PredicateCS: expr=ExpCS ';';

PrimitiveTypeDomainCS: 'primitive' 'domain' name=UnrestrictedName ':' type=TypedRefCS ';';

PropertyTemplateCS: (propertyId=[pivot::Property|UnrestrictedName]
	| ('opposite' '(' oppositePropertyId=PathNameCS ')'))
	'=' expression=ExpCSOrTemplateCS;
		
QueryCS: 'query' name=UnrestrictedName 
            '(' (inputParamDeclarations+=ParamDeclarationCS (',' inputParamDeclarations+=ParamDeclarationCS)*)? ')'
      	  ':' ownedType=TypedRefCS
            (';' | '{' expression=ExpCS '}');

RelationCS: top?='top'? 'relation' name=UnrestrictedName
   			 ('overrides' overrides=[qvtrelation::Relation|UnrestrictedName])?
               '{'
               varDeclarations+=VarDeclarationCS*
               (domains+=DomainCS | domains+=PrimitiveTypeDomainCS)+ 
               when=WhenCS? where=WhereCS?
               '}'
;

TemplateCS: (CollectionTemplateCS | ObjectTemplateCS) ('{' guardExpression=ExpCS '}')?;
	
TransformationCS: 'transformation' name=UnrestrictedName 
                     '(' modelDecls+=ModelDeclCS (',' modelDecls+=ModelDeclCS)* ')' 
                     ('extends' extends+=[qvtbase::Transformation|UnrestrictedName] (',' extends+=[qvtbase::Transformation|UnrestrictedName])*)?
                     '{' keyDecls+=KeyDeclCS* ( relation+=RelationCS | queries+=QueryCS )* '}'
;

UnitCS returns base::ImportCS:
	'import' (name=UnrestrictedName ':')? ownedPathName=URIPathNameCS ('::' isAll?='*')? ';';

VarDeclarationCS: varDeclarationIds+=VarDeclarationIdCS (',' varDeclarationIds+=VarDeclarationIdCS)* ':' type=TypedRefCS ';';

VarDeclarationIdCS: name=UnrestrictedName;

WhenCS returns PatternCS: {PatternCS} 'when' '{' predicates+=PredicateCS* '}';

WhereCS returns PatternCS: {PatternCS} 'where' '{' predicates+=PredicateCS* '}';
		
//<OCLExpressionCS> ::= <PropertyCallExpCS> 
//                    | <VariableExpCS>
//                    | <LiteralExpCS>
//                    | <LetExpCS>
//                    | <IfExpCS>
//                    | '(' <OCLExpressionCS> ')'
//                    | <template>  
//	notNameExpressionCS -> templateCS

ExpCSOrTemplateCS returns essentialocl::ExpCS: ExpCS | TemplateCS;

TypedTypeRefCS returns base::TypedTypeRefCS: ownedPathName=PathNameCS;

TypedRefCS returns base::TypedRefCS:
	PrimitiveTypeCS
|	CollectionTypeCS
|	TypedTypeRefCS
;

//ExpCS returns essentialocl::ExpCS:
//	InfixedExpCS
//|	TemplateCS;

UnrestrictedName returns ecore::EString:
	EssentialOCLUnrestrictedName
//|   '_'
|   'checkonly'
|   'default_values'
|   'domain'
|   'enforce'
|   'extends'
|   'implementedby'
|   'import'
|   'library'
|   'key'
|   'opposite'
|   'overrides'
|   'primitive'
|   'query'
|   'relation'
|   'replace'
|   'top'
|   'transformation'
|   'when'
|   'where'
;
//--	relationIdentifier -> '_'	
