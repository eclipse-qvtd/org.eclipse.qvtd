import 'Tree.ecore'::tree;
import 'TallTree.ecore'::talltree;
import 'Tree2TallTree.ecore'::tree2talltree;

transformation Tree2TallTree {
	check tree imports tree;
	enforce talltree imports talltree;
	tree2talltree imports tree2talltree;
}

map __root__ in Tree2TallTree {
    for node : tree::Node in tree.objectsOfKind(Node)->sortedBy(name) {
        call Node2MiddleNode {
            node := node;
        } 
    } 
    for node : tree::Node in tree.objectsOfKind(Node)->sortedBy(name) {
        call Edge2MiddleEdge {
            node := node;
            node2tallNode := node.Node2TallNode;
        } 
    } 
    for node2tallNode : tree2talltree::Node2TallNode in tree2talltree.objectsOfKind(Node2TallNode)->sortedBy(name) {
        call MiddleNode2TallNode {
            node2tallNode ?= node2tallNode;			--isPolled
        } 
    } 
}

map Node2MiddleNode in Tree2TallTree {
	in:tree node : Node; 
	new:tree2talltree node2tallNode : Node2TallNode;
	set node2tallNode.node := node;
	set node2tallNode.name := node.name;
}

map Edge2MiddleEdge in Tree2TallTree {
	in:tree node : Node;
	in:tree2talltree node2tallNode : Node2TallNode;
	check node.parent <> null;
	emit node2tallNode.parent := node.parent.Node2TallNode;
--		node2tallNode.name := node.name;
}

map MiddleNode2TallNode in Tree2TallTree {
	check tree2talltree {
		check Node2TallNode::tallNode, Node2TallNode::children
	} () {} 
	enforce talltree {
		check TallNode::height
	} () {}
	in:tree2talltree node2tallNode : Node2TallNode;
	var tallNode_name : String := node2tallNode.name;
	var tallNode_children : Set(TallNode) := node2tallNode.children?.tallNode->asSet();
	var tallNode_height : Integer := if node2tallNode.children->notEmpty() then node2tallNode.children.tallNode.height->max() + 1 else 0 endif;
	new:talltree tallNode : TallNode;
	emit node2tallNode.tallNode := tallNode;
	set	tallNode.name := tallNode_name;
	set	tallNode.children := tallNode_children;
	emit tallNode.height := tallNode_height;
}
